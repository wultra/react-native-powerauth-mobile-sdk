ext {
  //
  // Defaults for the standalone build
  //
  PowerAuthSdkRN_kotlinVersion = "1.7.0"
  PowerAuthSdkRN_minSdkVersion = 21
  PowerAuthSdkRN_targetSdkVersion = 33
  PowerAuthSdkRN_compileSdkVersion = 33
  PowerAuthSdkRN_ndkversion = "21.4.7075529"
  PowerAuthSdkRN_reactNativeVersion = "0.73.4"
}

buildscript {
  repositories {
    mavenCentral()
    google()
  }

  dependencies {
    classpath("com.android.tools.build:gradle:7.2.1")
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.ext["PowerAuthSdkRN_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.ext["PowerAuthSdkRN_" + name]).toInteger()
}

android {
  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenCentral()
  google()
}

dependencies {
  api "com.wultra.android.powerauth:powerauth-sdk:1.7.9"

  if (project == rootProject) {
    // The standalone build require to specify exact version of RN
    def rnVersion = getExtOrDefault("reactNativeVersion")
    implementation "com.facebook.react:react-android:${rnVersion}"
  } else {
    // This works for RN versions > 0.71
    implementation "com.facebook.react:react-android"
  }
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../lib/")
    libraryName = "PowerAuthReactNative"
    codegenJavaPackageName = "com.wultra.android.powerauth.reactnative"
  }
}
